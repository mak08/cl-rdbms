+-------------------------------------------------------------------------------
| Description
| License
| Last Modified  <michael 2018-01-14 17:47:54>
+-------------------------------------------------------------------------------

<query specification> ::=
        SELECT [ <set quantifier> ] <select list> <table expression>

<select list> ::=
        <asterisk>
     |  <select sublist> [ { <comma> <select sublist> }... ]

<select sublist> ::=
        <derived column>
     |  <qualifier> <period> <asterisk>

<derived column> ::= 
        <value expression> [ [ AS ] <column name> ]

<table expression> ::=
        <from clause> [ <where clause> ] [ <group by clause> ] [ <having clause> ]

<from clause> ::= 
        FROM <table reference> [ { <comma> <table reference> }... ]

<table reference> ::=
        <table name> [ [ AS ] <correlation name> [ <left paren> <derived column list> <right paren> ] ]
     |  <subquery> [ AS ] <correlation name> [ <left paren> <derived column list> <right paren> ]
     |  <joined table>

<subquery> ::= 
        <left paren> <query expression> <right paren>

<query expression> ::=
        <non-join query expression>
     |  <joined table>

<non-join query expression> ::=
        <non-join query term>
     |  <query expression> UNION  [ ALL ] [ <corresponding spec> ] <query term>
     |  <query expression> EXCEPT [ ALL ] [ <corresponding spec> ] <query term>

<query term> ::=
        <non-join query term>
     |  <joined table>

<non-join query term> ::=
        <non-join query primary>
     |  <query term> INTERSECT [ ALL ] [ <corresponding spec> ] <query primary>

<query primary> ::=
        <non-join query primary>
     |  <joined table>

<non-join query primary> ::=
        <simple table>
     |  <left paren> <non-join query expression> <right paren>

<simple table> ::=
        <query specification>
     |  <table value constructor>
     |  <explicit table>

<explicit table> ::= 
        TABLE <table name>

<corresponding spec> ::=
        CORRESPONDING [ BY <left paren> <corresponding column list> <right paren> ]

<corresponding column list> ::= 
        <column name list>

<derived column list> ::= 
        <column name list>

<column name list> ::=
        <column name> [ { <comma> <column name> }... ]

<joined table> ::=
        <cross join>
     |  <qualified join>
     |  <left paren> <joined table> <right paren>

<cross join> ::=
        <table reference> CROSS JOIN <table reference>

<qualified join> ::=
        <table reference> [ NATURAL ] [ <join type> ] JOIN  <table reference> [ <join specification> ]

<join specification> ::=
        <join condition>
     |  <named columns join>

<join condition> ::= 
        ON <search condition>

<named columns join> ::=
        USING <left paren> <join column list> <right paren>

<join type> ::=
        INNER
     |  <outer join type> [ OUTER ]
     |  UNION

<outer join type> ::=
        LEFT
     |  RIGHT
     |  FULL

<join column list> ::= 
        <column name list>
